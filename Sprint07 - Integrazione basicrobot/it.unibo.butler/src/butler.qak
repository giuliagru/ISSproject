System butler

Dispatch prepare : prepare(Cmd)
Dispatch clear : clear(Cmd)
Dispatch add : add(Foodcode, Quantity)

Dispatch stop : stop(Cmd)
Dispatch reactivate : reactivate(Cmd)

Dispatch robotstate : robotstate(Cmd)

Request exposefridgestate : exposefridgestate(Cmd)
Reply fridgestate: fridgestate(List)

//roomstate updates
Request exposeroomstate : exposeroomstate(Cmd)
Reply roomstate : roomstate(List)
Dispatch updateState : updateState(Action)

//robot-fridge interaction
Request check_food : check_food(Foodcode, Quantity)
Reply food_available : food_available(Foodcode, Quantity)
Reply food_notavailable : food_notavailable(Foodcode, Quantity)

Dispatch take_food : take_food(Foodcode, Quantity)
Dispatch take_foodlist : take_foodlist(List)

//to alert maitre
Dispatch food_notavailable : food_notavailable(Foodcode, Quantity)

//robot movements
Request goto : goto(Destination)
Reply arrivedat : arrivedat(Destination)

//just for testing
Dispatch test : test(0)

Event prepare_button : prepare_button(Cmd)
Event clear_button : clear_button(Cmd)
Event add_button : add_button(Foodcode, Quantity)
Event stop_button : stop_button(Cmd)
Event reactivate_button : reactivate_button(Cmd)
Event roomstate : roomstate(Cmd)

//basicrobot
Dispatch cmd       	: cmd(MOVE)
 
Request step       : step( TIME )	
Reply   stepdone   : stepdone(V)  
Reply   stepfail   : stepfail(DURATION, CAUSE)
  
Event   obstacle   : obstacle( ARG ) 

Context ctxrobot ip [host="127.0.0.1" port=8038]  
//Context ctxmaitre ip [host="localhost" port=8038]
//Context ctxfridge ip [host="localhost" port=8038]
Context ctxbasicrobot ip [host="localhost" port=8020]

ExternalQActor basicrobot context ctxbasicrobot

//ROBOT
QActor robot context ctxrobot{
	State s0 initial{
		println("Butler | Ready")
	}
	Goto waitCmd
		
	State waitCmd{}
	Transition to
		whenMsg prepare -> handleprepare
		whenMsg add -> handleadd
		whenMsg clear -> handleclear
		whenMsg stop -> handlestop
		whenMsg robotstate -> exposestate
	
	State handleprepare{
		onMsg (prepare: prepare(X)){
			println("Butler | PREPARE started")
			updateResource [# "handlingprepare" #]
			forward robotexecutor -m prepare : prepare($payloadArg(0))
		}
	}
	Goto waitCmd
	
	State handleadd{
		onMsg (add: add(Foodcode, Quantity)){
			println("Butler | ADD started")
			updateResource [# "handlingadd" #]
			forward robotexecutor -m add : add($payloadArg(0), $payloadArg(1))
//			println("Butler | Requirement: ADD check food")
//			request fridge -m check_food : check_food($payloadArg(0), $payloadArg(1))
		}
	}
	Goto waitCmd
/*	Transition t0  
		whenReply food_available -> reactivated
		whenReply food_notavailable -> alertMaitre
		
	State alertMaitre{
		onMsg (food_notavailable : food_notavailable(Foodcode, Quantity)){
			println("Butler | Requirement: ALERT maitre that food isn't available")
			forward maitre -m food_notavailable : food_not_available($payloadArg(0), $payloadArg(1))
		}
	}Goto waitCmd
*/	
	State handleclear{
		onMsg (clear: clear(X)){
			println("Butler | CLEAR started")
			updateResource [# "handlingclear" #]
			forward robotexecutor -m clear : clear($payloadArg(0))
		}
	}
	Goto waitCmd
			
	State handlestop{
		onMsg(stop:stop(X)){
			updateResource [# "stopped" #]
			println("Butler | Requirement: STOP action")
		
		}
	}
	Transition t0 whenMsg reactivate -> handlereactivate
	
	State handlereactivate{
		onMsg(reactivate:reactivate(X)){
			updateResource [# "reactivated" #]
			println("Butler | Requirement: CONTINUE action")
		
		}
	}Goto waitCmd
	 
 	
 	State exposestate{
 		updateResource [# "exposestate" #]
		println("Butler | Requirement: EXPOSE robot state")
 	}Goto waitCmd	
}


QActor robotexecutor context ctxrobot{
	State s0 initial{
		solve( consult("preparerequirements.pl") )
	}	Goto waitCmd
	
	State waitCmd{
		updateResource [# "athome" #]
	}
	Transition t0
		whenMsg prepare->handleprepare
		whenMsg add -> handleadd
		whenMsg clear -> handleclear
	
	State handleprepare{
		onMsg (prepare: prepare(X)){
			updateResource [# "startprepare" #]
			println("Robot executor | Prepare: start")
			request robotmover -m goto : goto(pantry)
		}
	}Transition t1
		whenReply arrivedat-> atpantry
		
	State atpantry{
		onMsg(arrivedat:arrivedat(pantry)){	
			updateResource [# "atpantry" #]
			forward roomstate -m updateState : updateState(preparedishes) 
			println("Robot executor | Prepare: dishes taken")
			request robotmover -m goto : goto(table)
		}
	}Transition t2
		whenReply arrivedat-> attable_dishes
		
	State attable_dishes{
		onMsg(arrivedat:arrivedat(table)){
			updateResource [# "attable_dishes" #]
			forward roomstate -m updateState : updateState(action(put, table, dishes))
			println( "Robot executor | Prepare: dishes on table " )
			request robotmover -m goto:goto(fridge)
		}
	}Transition t3
		whenReply arrivedat -> atfridge 
	
	State atfridge{
		onMsg(arrivedat:arrivedat(fridge)){	
			updateResource [# "atfridge" #]
			solve(food(F))
			forward fridge -m take_foodlist : take_foodlist($getCurSol("F"))
			
			forward roomstate -m updateState : updateState(preparefood)
			println("Robot executor | Prepare: food taken")
			request robotmover -m goto : goto(table)		
		}
		
	}Transition t5
		whenReply arrivedat -> attable_food
		
	State attable_food{
		onMsg(arrivedat:arrivedat(table)){	
			updateResource [# "attable_food" #]
			forward roomstate -m updateState : updateState(action(put, table, food))
			println("Robot executor | Prepare/Add: food on table")		
			request robotmover -m goto : goto(home)
		}	
	}Transition t6
		whenReply arrivedat -> endTask
	
	State endTask {
		updateResource [# "endtask" #]
		println("Robot executor | Action: end")	
	}Goto waitCmd 
	
	State handleadd{
		onMsg (add: add(Food, Quantity)){
			updateResource [# "startadd" #]
			println("Robot executor | Add: start")
			request fridge -m check_food : check_food( $payloadArg(0), $payloadArg(1))
		}

	}Transition t7 
		whenReply food_available -> tofridge
		whenReply food_notavailable -> alertMaitre
	
	State tofridge{
		onMsg (food_available : food_available(Foodcode, Quantity)){
			updateResource [# "foodavailable" #]
			println("Robot executor | Add: ${payloadArg(0)} ${payloadArg(1)} available")
			
			//so the robot is able to remember these arguments in the next state
			solve(assert(remember($payloadArg(0), $payloadArg(1))))	
			request robotmover -m goto:goto(fridge)
				
		}
	}Transition t8
		whenReply arrivedat -> takefood
		
	State takefood{
		onMsg(arrivedat : arrivedat(fridge)){
			updateResource [# "atfridge" #]
			solve(retract(remember(F, Q)))
			ifSolved {  
				[#
					var Foodcode = getCurSol("F").toString() 
					var Quantity = getCurSol("Q").toString() 
		        #]
		        forward fridge -m take_food : take_food($Foodcode, $Quantity)
				forward roomstate -m updateState : updateState(action(take, fridge, food, $Foodcode, $Quantity))
			}	
			println("Robot executor | Add: food taken")
			request robotmover -m goto : goto(table)
		}
		
	}Transition t10
		whenReply arrivedat -> attable_food
		
	State alertMaitre{
		onMsg (food_notavailable : food_notavailable(Foodcode, Quantity)){
			updateResource [# "foodnotavailable" #]
			println("Robot executor | Add: ${payloadArg(0)} ${payloadArg(1)} not available")
			forward maitre -m food_notavailable : food_notavailable($payloadArg(0), $payloadArg(1)) 
		}
	}Goto endTask
	
	State handleclear{
		onMsg (clear: clear(X)){
			updateResource [# "startclear" #]
			println("Robot executor | Clear: start")
			request robotmover -m goto : goto(table)
		}
	}Transition t11
		whenReply arrivedat -> cleartable_food
	
	State cleartable_food{
		onMsg(arrivedat:arrivedat(table))	{
			updateResource [# "attable" #]
			forward roomstate -m updateState : updateState(action(take, table, food))
			println("Robot executor | Clear: food taken from table")
			request robotmover -m goto : goto(fridge)
			
		}
	}Transition t12
		whenReply arrivedat -> leftoverstofridge
	
	State leftoverstofridge{
		onMsg(arrivedat:arrivedat(fridge)){	
			updateResource [# "atfridge" #]
			forward roomstate -m updateState : updateState(action(put, fridge, food))
			println("Robot executor | Clear: leftovers in the fridge")
			request robotmover -m goto : goto(table)
		}
	}Transition t13
		whenReply arrivedat -> cleartable_dishes
		
	State cleartable_dishes{
		onMsg(arrivedat:arrivedat(table))	{
			updateResource [# "attable" #]
			forward roomstate -m updateState : updateState(action(take, table, dishes))
			println("Robot executor | Clear: dishes taken from table")
			request robotmover -m goto : goto(dishwasher)
		}
	}Transition t14
		whenReply arrivedat -> dishestodishwasher
	
	State dishestodishwasher{
		onMsg(arrivedat:arrivedat(dishwasher)){
			updateResource [# "atdishwasher" #]
			forward roomstate -m updateState : updateState(action(put, dishwasher, dishes))
			println("Robot executor | Clear: dishes in the dishwasher")
			request robotmover -m goto : goto(home)			
		}	
	}Transition t15
		whenReply arrivedat -> endTask
	
}

QActor robotmover context ctxrobot{
	State s0 initial{
		
	}Goto waitCmd
	
	State waitCmd{ 
		updateResource [# "notmoving" #]
	}
	Transition t0
		whenRequest goto -> moving	
		
	State moving {
		onMsg(goto: goto(Destination)){
			updateResource [# "moving" #]
			println("Robot mover	| going to ${payloadArg(0)}")
			forward basicrobot -m cmd : cmd(l)
			replyTo goto with arrivedat: arrivedat($payloadArg(0))
		}
	}Goto waitCmd
}

QActor roomstate context ctxrobot{
	State s0 initial{
		solve( consult("sysRules.pl")	 )
		solve( consult("roomstate.pl")	 )
		solve( consult("preparerequirements.pl") )
	}Goto waitStateChange
	
	State waitStateChange{	}
		Transition t1
		whenRequest exposeroomstate -> exposeroomstate
		whenMsg updateState -> update
	
	State exposeroomstate{
		updateResource [# "exposeroomstate" #]
		onMsg(exposeroomstate : exposeroomstate(Cmd)){
			solve(roomstate(St))
			ifSolved{
				replyTo exposeroomstate with roomstate : roomstate($getCurSol("St"))
			}
		}
	}Goto waitStateChange
	
	State update{
		updateResource [# "updateroomstate" #]
		onMsg(updateState : updateState(preparedishes)){
			solve(dishes(D))
			[#
				var Sol = getCurSol("D")
			#]
			solve(action(take,pantry,dishes,$Sol))	
		}
		onMsg(updateState : updateState(preparefood)){
			solve(food(F))
			[#
				var Sol = getCurSol("F")
			#]
			solve(action(take,fridge,food,$Sol))			
		}

		onMsg(updateState : updateState(action(A,B,C))){
			solve($payloadArg(0))
		
		}
		onMsg(updateState : updateState(action(A,B,C,D))){
			solve($payloadArg(0))
			
		}
		onMsg(updateState : updateState(action(A,B,C,D,E))){
			solve($payloadArg(0))
		}
		
		solve(roomstate(L))
		println("Room state | room has ${getCurSol(\"L\")}")
	}Goto waitStateChange
}

//FRIDGE
QActor fridge context ctxrobot{
	State s0 initial{
		solve( consult("sysRules.pl")	 )
		solve( consult("fridgestate.pl")	 )
		println("Fridge | Ready")                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                   
	}Goto waitCmd
	
	State waitCmd{	}
	Transition t0
		whenRequest check_food -> check
		whenRequest exposefridgestate -> exposestate
		whenMsg take_food -> take
		whenMsg take_foodlist -> take
	
	//check food availability
	State check{
		updateResource [# "checkfood" #]
		delay 500
		onMsg(check_food : check_food(Foodcode, Quantity)){
			println("Fridge | checking presence of food: ${payloadArg(0)} quantity: ${payloadArg(1)}")
			solve(check($payloadArg(0),$payloadArg(1) ))
			
			ifSolved {replyTo check_food with food_available : food_available($payloadArg(0),$payloadArg(1))}
			else {replyTo check_food with food_notavailable : food_notavailable($payloadArg(0),$payloadArg(1))}
		}
	}
	Goto waitCmd
	
	//take food 
	State take{
		updateResource [# "takefood" #]
		onMsg(take_food: take_food(Foodcode, Quantity)){
			solve(action(take, fridge, food, $payloadArg(0), $payloadArg(1)))
			ifSolved {
				println("Fridge | ${payloadArg(1)} ${payloadArg(0)} taken")
			}			
		}
		
		onMsg(take_foodlist: take_foodlist(L)){
			[# var Food: String = payloadArg(0);
				 var Foodstring = Food.replace(",(","(") #] //to remove the extra comma from prolog toString
			//println(Foodstring)
			solve(text_term($Foodstring,E))
			ifSolved{
				[#var Sol = getCurSol("E");
						var Solstring = Sol.toString()#]
				solve(action(take, fridge, food, $Sol))
				ifSolved {
					println("Fridge | $Food taken")					
				}
			}			
		}
	}Goto waitCmd
	
	
	//expose fridge state to maitre
	State exposestate{
		updateResource [# "exposestate" #]
		solve(state(fridge, List))
		ifSolved{
			println("Fridge | state: ${getCurSol(\"List\")}")
			replyTo exposefridgestate with fridgestate : fridgestate($getCurSol("List")) 
		}
	}	
	Goto waitCmd	
}

//MAITRE
QActor maitre context ctxrobot {
	State s0 initial{
		println("Maitre | Ready")
	}
	Goto waitCmdPrepare
	
	State waitCmdPrepare{}
	Transition t0
		whenEvent prepare_button -> prepare
	
	State waitCmd{}
	Transition t1
		whenEvent add_button -> add
		whenEvent clear_button -> clear
		whenEvent stop_button -> stoprobot
		whenEvent roomstate -> consultroom
		whenMsg food_notavailable -> handlerobotmsg
		
	State prepare {
		onMsg(prepare_button: prepare_button(X)){
			println("Maitre | Requirement: PREPARE")
			updateResource [# "prepare" #]
			forward robot -m prepare : prepare(Cmd)
		}
	}	
	Goto waitCmd
		
	State add {
		onMsg(add_button: add_button(Foodcode, Quantity)){
			println("Maitre | Requirement: ADD")
			updateResource [# "add" #]
			forward robot -m add : add($payloadArg(0), $payloadArg(1))
		}
	}	
	Goto waitCmd
		
	State clear {
		onMsg(clear_button: clear_button(X)){
			println("Maitre | Requirement: CLEAR")
			updateResource [# "clear" #]
			forward robot -m clear : clear(Cmd)
		}
	}
	Goto waitCmdPrepare
	
	State stoprobot{
		println("Maitre | Requirement: STOP")
		updateResource [# "stoprobot" #]
		forward robot -m stop : stop(Action)
	}
	Transition t1
	whenEvent reactivate_button -> reactivaterobot
	
	State reactivaterobot{
		println("Maitre | Requirement: REACTIVATE")
		updateResource [# "reactivaterobot" #]
		forward robot -m reactivate : reactivate(Action)
	}
	Goto waitCmd
	
	State consultroom{
		println("Maitre | Requirement: CONSULT ROOM")
		updateResource [# "consultroom" #]
		request roomstate -m exposeroomstate : exposeroomstate(Cmd)
	}
	Transition t2
	whenReply roomstate -> waitroom
	

	State waitroom{
		onMsg(roomstate: roomstate(List)){
			println("Maitre | room state received: ${payloadArg(0)}")
		}
	}Goto waitCmd
	
	State handlerobotmsg{
		onMsg (food_notavailable: food_notavailable(Foodcode, Quantity)){
			println("Maitre | knows ${payloadArg(0)} isn't available")
		}
	}
	Goto waitCmd
}

QActor user context ctxrobot{
	State s0 initial{
	} Transition t0 
		whenTime 3000 -> emitCmd
		whenMsg  test -> end
	
	State emitCmd{	
		delay 2000
		emit prepare_button : prepare_button(clicked)

        delay 10000
        emit add_button : add_button(pizza,1)
        
        delay 10000
        emit add_button : add_button(cheese,1)
         
        delay 10000
        emit clear_button : clear_button(clicked)
	}
	
	State end {
		terminate 0
	}
}